import org.hidetake.gradle.swagger.generator.GenerateSwaggerUI
import org.springframework.boot.gradle.tasks.bundling.BootJar

buildscript {
	ext {
		restdocsApiSpecVersion = '0.16.2'
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.8'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
	id 'org.hidetake.swagger.generator' version '2.18.2'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'com.developlife'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

swaggerSources {
	sample {
		setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
	}
}

openapi3 {
	def Properties properties = new Properties()
	InputStream inputStream = new FileInputStream(project.rootProject.file('src/main/resources/application.properties'))
	properties.load(inputStream)
	setServer(properties.getProperty("openapi3.hostname", "http://localhost:8080"))
	title = "ReviewTwits API Documentation"
	description = "reviewtwits에서 사용하는 API 문서입니다."
	version = "1.0.1"
	format = "yaml" // default json
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'org.springframework.boot:spring-boot-starter-security:3.0.2'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation 'org.jsoup:jsoup:1.15.4'
	implementation 'commons-fileupload:commons-fileupload:1.4'
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	implementation 'org.springframework.boot:spring-boot-starter-mail:2.7.6'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.2'
	implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '4.8.1'
	implementation 'io.github.bonigarcia:webdrivermanager:5.3.1'
	implementation ('com.github.javafaker:javafaker:1.0.2') { exclude module: 'org.yaml' }
	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.429'
	implementation 'com.amazonaws:aws-java-sdk-secretsmanager:1.12.429'
	implementation 'com.amazonaws:aws-encryption-sdk-java:2.4.0'
	implementation 'org.springframework.data:spring-data-commons:2.7.9'
	//querydsl 추가
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
	implementation 'org.springframework.boot:spring-boot-starter-data-redis:2.7.8'
	testImplementation 'org.projectlombok:lombok:1.18.22'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:3.0.4'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "com.epages:restdocs-api-spec-restassured:${restdocsApiSpecVersion}"
	testImplementation 'io.rest-assured:rest-assured:5.3.0'
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured:2.0.6.RELEASE'
	swaggerUI 'org.webjars:swagger-ui:4.11.1'
	modules {
		module("org.codehaus.groovy:groovy") {
			replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
		}
		module("org.codehaus.groovy:groovy-xml") {
			replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
		}
	}
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}

compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath
}
//querydsl 추가 끝

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(GenerateSwaggerUI) {
	dependsOn 'openapi3'
}

// 생성된 SwaggerUI 를 jar 에 포함시키기 위해 build/resources 경로로 로 복사
tasks.register('copySwaggerUI', Copy) {
	dependsOn "generateSwaggerUISample"
	def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()
	from("${generateSwaggerUISampleTask.outputDir}")
	into("${project.buildDir}/resources/main/static/docs")
}

// bootJar 실행 전, copySwaggerUI 를 실행하도록 설정
tasks.withType(BootJar) {
	dependsOn 'copySwaggerUI'
}